<?xml version="1.0" encoding="utf-8"?>
<mx:VBox
    xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:mx="library://ns.adobe.com/flex/mx"
	xmlns:flexlib="flexlib.containers.*"
	xmlns:controls="net.systemeD.controls.*"
	xmlns:potlatch2="net.systemeD.potlatch2.*"
    xmlns:sidepanel="net.systemeD.potlatch2.panels.*"
	horizontalScrollPolicy="off"
    backgroundColor="white"
    initialize="loadFeatures()">

<mx:ViewStack id="sidebar" width="100%" height="100%" creationPolicy="all">

  <!-- Drag & drop icon panel -->

  <mx:VBox id="dndPanel" width="100%" height="100%" horizontalScrollPolicy="off" styleName="dndPanelVbox">
    <mx:Text id="dndPanelText" text="{dndPrompt}" width="100%" styleName="helpInfo" />
	<mx:Repeater id="dndRep" dataProvider="{MapFeatures.getInstance().getCategoriesForType('point')}" styleName="dndRepeater">
		<mx:HBox width="100%" styleName="dndPanelCategory">
			<mx:Label text="{dndRep.currentItem.name}:" styleName="dndPanelCategoryLabel"/>
		</mx:HBox>

		<mx:TileList dataProvider="{dndRep.currentItem.getFeaturesForType('point', true)}" width="100%" height="1"
			             rowHeight="32" columnWidth="32" updateComplete="resizePOIGrid(event)" styleName="dndPanelTileList">
			<mx:itemRenderer>
				<fx:Component>
					<mx:VBox toolTip="{data.name}">
						<fx:Script><![CDATA[
							import mx.events.DragEvent;
							import mx.managers.DragManager;
							import mx.core.DragSource;

							private function dragPOI(event:MouseEvent, tags:Array):void {
								if (outerDocument.controller.map.dragstate==outerDocument.controller.map.DRAGGING) return;

								// Get the drag initiator component from the event object.
								var dragInitiator:Image = event.currentTarget as Image;
								var dragSource:DragSource = new DragSource();
								dragSource.addData(tags, 'tags');
								dragSource.addData(event.target.parent.parent.parent.parent, 'container');

								var dragProxy:Image = new Image();
								dragProxy.source = dragInitiator.source;
								dragProxy.width = dragInitiator.width;   // must set width and height explicitly
								dragProxy.height = dragInitiator.height; // for non-embedded images
								DragManager.doDrag(dragInitiator, dragSource, event, dragProxy);
							}
						]]></fx:Script>
						<mx:Image id="foo" source="{data.dndimage}" height="24" width="24" mouseMove="dragPOI(event, data.tags)" toolTip="{data.name}" />
					</mx:VBox>
				</fx:Component>
			</mx:itemRenderer>
		</mx:TileList>
	</mx:Repeater>
  </mx:VBox>

  <!-- Standard tagging panel -->

  <mx:VBox id="tagsPanel" width="100%" height="100%" creationPolicy="auto">
    <mx:ViewStack id="stack" width="100%" height="100%">
      <mx:VBox width="100%" height="100%" id="editorContainer" creationComplete="initEditorStackUIs()" styleName="dndPanelVbox">
        <mx:VBox width="100%" verticalGap="1" styleName="dndTagHeader">
          <mx:HBox width="100%" id="iconContainer" styleName="featureSelector">
            <mx:Image id="iconImage"/>
            <mx:Text condenseWhite="true" width="100%" id="iconText" styleName="dndIconText"/>
          </mx:HBox>
          <mx:HBox width="100%">
            <mx:PopUpButton id="popupChange" openAlways="true" width="100%" styleName="dndTagPopUpMenu" closeDuration="0" />
            <mx:LinkButton icon="@Embed('../../../embedded/information.svg')" click="openDescription()" id="helpLabel" styleName="helpInfo"/>
          </mx:HBox>
        </mx:VBox>
      </mx:VBox>

      <mx:VBox width="100%" height="100%" label="Members" id="membersVBox" initialize="checkMembers()" verticalGap="1">
        <mx:Label id="membersText" text="Relation Members"/>
        <mx:DataGrid editable="true" width="100%" height="100%" id="membersGrid"
                      dragEnabled="true" dragMoveEnabled="true" dropEnabled="true">
          <mx:columns>
            <mx:DataGridColumn editable="false" dataField="type" headerText="Type"/>
            <mx:DataGridColumn editable="false" dataField="id" headerText="ID"/>
            <mx:DataGridColumn editable="true" dataField="role" headerText="Role">
				<mx:itemEditor><fx:Component><mx:TextInput restrict="&#x0020;-&#x10FFFF;" /></fx:Component></mx:itemEditor>
			</mx:DataGridColumn>
          </mx:columns>
        </mx:DataGrid>
      </mx:VBox>

      </mx:ViewStack>

  </mx:VBox>

  <!-- Multiple selection -->


  <!-- Generic marker panel -->

  <mx:VBox id="markerPanel" width="100%" height="100%" horizontalScrollPolicy="off" styleName="dndPanelVbox">
    <sidepanel:MarkerPanel id="markerPanelContents" width="100%"/>
  </mx:VBox>

  <!-- Bug editing panel -->

  <mx:VBox id="bugPanel" width="100%" height="100%" horizontalScrollPolicy="off" styleName="dndPanelVbox">
    <sidepanel:BugPanel id="bugPanelContents" width="100%"/>
  </mx:VBox>

  <!-- Background layer selection -->

  <mx:VBox id="backgroundPanel" width="100%" height="100%" horizontalScrollPolicy="off" styleName="dndPanelVbox">
    <sidepanel:BackgroundPanel id="backgroundPanelContents" width="100%"/>
  </mx:VBox>

  <!-- merge tags from background layer -->

  <mx:VBox id="backgroundMergePanel" width="100%" height="100%" horizontalScrollPolicy="off" styleName="dndPanelVbox">
    <sidepanel:BackgroundMergePanel id="backgroundMergePanelContents" width="100%" />
  </mx:VBox>

</mx:ViewStack>

  <fx:Script><![CDATA[
      import net.systemeD.halcyon.connection.*;
      import net.systemeD.halcyon.MapPaint;
      import net.systemeD.potlatch2.EditController;
      import net.systemeD.potlatch2.mapfeatures.*;
      import net.systemeD.potlatch2.mapfeatures.editors.*;
      import net.systemeD.potlatch2.utils.*;
      import net.systemeD.controls.CollapsiblePanel;

      import mx.collections.*;
      import mx.containers.*;
      import mx.events.*;
      import mx.core.*;
      import mx.managers.PopUpManager;
      import mx.controls.Menu;
      import mx.controls.Alert;
      import flash.geom.Point;
      import flash.net.*;
      import mx.events.DragEvent;
      import mx.managers.DragManager;
      import mx.core.DragSource;
      import mx.controls.TabBar;
//    import flexlib.containers.SuperTabNavigator;

      [Bindable] [Embed(source="../../../embedded/tab_basic.png"       )] private var tabIconBasic:Class;
      [Bindable] [Embed(source="../../../embedded/tab_address.png"     )] private var tabIconAddress:Class;
      [Bindable] [Embed(source="../../../embedded/tab_cycle.png"       )] private var tabIconCycle:Class;
      [Bindable] [Embed(source="../../../embedded/tab_details.png"     )] private var tabIconDetails:Class;
      [Bindable] [Embed(source="../../../embedded/tab_restrictions.png")] private var tabIconRestrictions:Class;
      [Bindable] [Embed(source="../../../embedded/tab_transport.png"   )] private var tabIconTransport:Class;
      [Bindable] [Embed(source="../../../embedded/tab_walk.png"        )] private var tabIconWalk:Class;
      private var tabIcons:Object= { Basic:tabIconBasic, Details:tabIconDetails, Address:tabIconAddress, Walk:tabIconWalk, Cycle:tabIconCycle, 
	                                 Transport:tabIconTransport, Restrictions:tabIconRestrictions};

      [Bindable]
      public var dndPrompt:String="Add new points by dragging them onto the map";

      private var editorStackTabNavigator:TabNavigator;
      private var editorStackAccordion:Accordion;
      [Bindable] private var editorStack:Container;

      public var mapFeatures:MapFeatures;
      public var controller:EditController;
      private var selectedEntity:Entity;
      private var connection:Connection;
      private var currentCategorySelector:CategorySelector;
      private var categorySelectors:Object = {};	// hash of categorySelectors for each limitType
      private var feature:Feature = null;

      private var rowData:Object;		// relation membership reference, needed so it's accessible from relation actions menu

	  public function setEntity(entities:Array, layer:MapPaint=null):void {
		UIComponent.suspendBackgroundProcessing();
		connection=null;

		var firstSelected:Entity=null;
		if (entities.length==1) { firstSelected=entities[0]; }

		if (selectedEntity!=firstSelected && selectedEntity!=null) {
			selectedEntity.removeEventListener(Connection.TAG_CHANGED, tagChanged);
			selectedEntity.removeEventListener(Connection.ADDED_TO_RELATION, addedToRelation);
			selectedEntity.removeEventListener(Connection.REMOVED_FROM_RELATION, removedFromRelation);
		}

		if (entities.length==0) {
			// Nothing selected, so show drag-and-drop panel
			sidebar.selectedChild = dndPanel;

		} else if (entities.length==1) {
			// Single entity selected, so show tag panel
			selectedEntity=firstSelected;
			connection=firstSelected.connection;
			if (selectedEntity!=null) { selectedEntity.addEventListener(Connection.TAG_CHANGED, tagChanged); }
			//if (advancedID!=null) { setupAdvanced(firstSelected); }
			if (firstSelected is Relation) { stack.addChild(membersVBox); }
            if (selectedEntity is Marker && connection is BugConnection) {
              bugPanelContents.init(selectedEntity, BugConnection(connection));
              sidebar.selectedChild = bugPanel;
            } else if (selectedEntity is Marker) {
              markerPanelContents.init(selectedEntity, layer);
              sidebar.selectedChild = markerPanel;
            } else if (connection is SnapshotConnection) {
              backgroundPanelContents.init(selectedEntity, layer);
              sidebar.selectedChild = backgroundPanel;
            } else {
              refreshFeatureIcon();
              initialiseEditors();
              sidebar.selectedChild = tagsPanel;
            }

        } else if (entities.length==2
                      && xor(entities[0].connection is SnapshotConnection, entities[1].connection is SnapshotConnection)
                      && xor(!controller.map.getLayerForEntity(entities[0]).isBackground, !controller.map.getLayerForEntity(entities[1]).isBackground) ) {
            backgroundMergePanelContents.init(entities);
            sidebar.selectedChild = backgroundMergePanel;

		} 
		UIComponent.resumeBackgroundProcessing();
      }

      private function xor(a:Boolean, b:Boolean):Boolean {
          return ( a || b) && !(a && b);
      }

      private function refreshFeatureIcon():void {
          var oldFeature:Feature = feature;
          feature = selectedEntity == null ? null : mapFeatures.findMatchingFeature(selectedEntity);
          if ( feature != oldFeature ) {
              if ( oldFeature != null )
                  oldFeature.removeEventListener("imageChanged", featureImageChanged);
              if ( feature != null )
                  feature.addEventListener("imageChanged", featureImageChanged);
          }
          setCategorySelector(selectedEntity, feature);
      }

      private function featureImageChanged(event:Event):void {
          setCategorySelector(selectedEntity, feature);
      }

      /** Set the icon, categorySelector and help text for the current entity. */
      private function setCategorySelector(entity:Entity, feature:Feature):void {
			// Remove the "user has selected something" event listener from previous categorySelector,
			// and make it invisible because Flex is crap at updating click areas otherwise
			if (currentCategorySelector) { 
				currentCategorySelector.removeEventListener("selectedType", changeFeatureType);
				currentCategorySelector.visible=false;
			}
			
			// Have we cached the categorySelector for this limitType? If not, create one
			var lt:String=limitType(entity);
			if (!categorySelectors[lt]) {
				categorySelectors[lt]=new CategorySelector();
				categorySelectors[lt].setLimitTypes(lt);
			}
			currentCategorySelector=categorySelectors[lt];
			currentCategorySelector.addEventListener("selectedType", changeFeatureType, false, 0, true);

			// Update surrounding icon/text display
			if (feature) {
				iconImage.source = feature.image;
				iconText.htmlText = feature.htmlDetails(entity);
				popupChange.label = feature.name;
				helpLabel.visible = feature.hasHelpURL();
				currentCategorySelector.setSelectedFeature(feature);
			} else {
				iconImage.source = null;
				popupChange.label = "unknown";
				helpLabel.visible = false;
				if (entity==null) {
					iconText.htmlText = "<i>Nothing selected</i>";
				} else if (entity.hasTags()) {
					iconText.htmlText = "<b>Not recognised</b><br/><font size='10pt'>Try looking at the tags under the advanced properties</font>";
				} else {
					iconText.htmlText = "<b>No tags set</b><br/><font size='10pt'>Please use the menu below to define what this "+entity.getType()+" is</font>";
				}
				currentCategorySelector.setSelectedFeature(null);
			}

			// Set it as the popup, and make sure it's visible
			popupChange.popUp=currentCategorySelector;
			currentCategorySelector.visible=true;
      }

	  private function isMultipleEditable(entities:Array):Boolean {
		for each(var entity:Entity in entities) {
			if(!(entity is Node || entity is Way))
				return false;
		}
		return true;
	  }

      private function limitType(entity:Entity):String {
          if      (entity is Node    ) return "point";
          else if (entity is Way     ) return Way(entity).isArea() ? "area" : "line";
          else if (entity is Relation) return "relation";
          return null;
      }

      private var tabComponents:Object = {};
      private var subpanelComponents:Object = {};

      private function initialiseEditors():void {
		editorStack.removeAllChildren();
		if ( selectedEntity == null || feature == null )
			return;

		var basicEditorBox:VBox = createEditorBox();
		basicEditorBox.label = "Required";
		//basicEditorBox.icon=tabIconBasic;
		editorStack.addChild(basicEditorBox);

		var tabs:Object = {};
		var tabList:Array = [];
		tabComponents = {};
		var subpanels:Object = {};
		subpanelComponents = {};

		// First create the tabs
		for each (var factory:EditorFactory in feature.editors) {
			var category:String = factory.category;
			if (category!='') {
				var tab:VBox = tabs[category];
				if ( tab == null) {
					tab = createEditorBox();
					tab.label = category;
					if (tabIcons[category]) tab.icon=tabIcons[category];
					tabs[category] = tab;
					tabList.push(tab);	
				}
			}
		}

		// Put the tabs on-screen in order
		tabList.sort(sortCategories,16);
		for each (tab in tabList) {
			editorStack.addChild(tab);
			tabComponents[tab] = [];
		}

		// Then add the individual editors to them
		for each (factory in feature.editors) {

			// Add to basic editor box first
			if ( factory.presence.isEditorPresent(factory, selectedEntity, null) ) {
				var editor:DisplayObject = factory.createEditorInstance(selectedEntity);
				if (editor) basicEditorBox.addChild(editor);
			}

			// Then prepare to add to category panel
			category=factory.category;
			if (factory.category=='') continue;
			var catEditor:DisplayObject = factory.createEditorInstance(selectedEntity);
			if (!catEditor) continue;
			tab=tabs[category];
			
			// Create subcategory panel if needed
			if (factory.subcategory) {
				var subcategory:String = factory.subcategory;
				if (!subpanels[category]) { subpanels[category]={}; }
				var subpanel:CollapsiblePanel = subpanels[category][subcategory];
				if (!subpanel) {
					subpanel=new CollapsiblePanel(false);
					subpanel.percentWidth=100;
					subpanel.styleName="subcategoryPanel";
					subpanel.title=subcategory;
					subpanels[category][subcategory]=subpanel;
					tabComponents[tab].push(subpanel);
				}
				subpanel.addChild(catEditor);
			} else {
				tabComponents[tab].push(catEditor);
			}
   		}
      }

	// ** FIXME: Order probably shouldn't be hard-coded, but configurable
	private static var categoryOrder:Array=["Restrictions","Transport","Cycle","Walk","Address","Details"];	// reverse order
	private function sortCategories(a:VBox,b:VBox):int {
		var a1:int=categoryOrder.indexOf(a.label);
		var a2:int=categoryOrder.indexOf(b.label);
		if (a1<a2) { return 1; }
		else if (a1>a2) { return -1; }
		return 0;
	}
	
      private function createEditorBox():VBox {
          var box:VBox = new VBox();
          box.percentWidth = 100;
          box.percentHeight = 100;
          box.styleName = "dndEditorContainer";
          return box;
      }

      private function ensureEditorsPopulated(tab:VBox):void {
          var components:Array = tabComponents[tab];
          if ( components == null || tab == null || tab.numChildren >= components.length )
              return;
          for each (var component:DisplayObject in components ) {
              tab.addChild(component);
          }
      }

	private function initEditorStackUIs():void {
		editorStackTabNavigator = new TabNavigator();
//		editorStackTabNavigator.allowTabSqueezing=false;
//		editorStackTabNavigator.minTabWidth=10;
//		editorStackTabNavigator.closePolicy="close_never";
//		editorStackTabNavigator.scrollSpeed=20;
		editorStackTabNavigator.creationPolicy="auto";
		editorStackTabNavigator.percentWidth=100;
		editorStackTabNavigator.percentHeight=100;
		editorStackTabNavigator.styleName="dndStackTab";
//		editorStackTabNavigator.popUpButtonPolicy="off"


		editorStackAccordion = new Accordion();
		editorStackAccordion.percentWidth=100;
		editorStackAccordion.percentHeight=100;
		editorStackAccordion.creationPolicy="auto";
		editorStackAccordion.styleName="dndStackAccordion";
		/* FIXME: the accordion icons should be right-aligned. See:
		http://www.kristoferjoseph.com/blog/2008/11/06/positioning-the-flex-accordion-header-icon
		http://blog.flexexamples.com/2007/09/13/changing-text-alignment-in-an-flex-accordion-header/
		*/

		setEditorStackUI(true);
	}

	private function setEditorStackUI(isTabbed:Boolean):void {
		var children:Array=[]; var i:uint;

		if (editorStack) {
			// blank existing component
			editorStack.removeEventListener("change",editorStackUIChange);
			editorStack.removeEventListener("updateComplete",editorStackUIUpdate);
			editorStack.removeAllChildren();
			editorContainer.removeChildAt(1);
		}

		// replace with new component
		editorStack = (isTabbed ? editorStackTabNavigator : editorStackAccordion) as Container;
		editorContainer.addChild(editorStack);

		// re-add children and listeners
		initialiseEditors();
		editorStack.addEventListener("change",editorStackUIChange);
		editorStack.addEventListener("updateComplete",editorStackUIUpdate);
	}

	private function editorStackUIChange(event:Event):void {
		ensureEditorsPopulated(IndexChangedEvent(event).relatedObject as VBox);
	}

	private function editorStackUIUpdate(event:Event):void {
		if (editorStack is TabNavigator) {
			var e:TabNavigator = editorStack as TabNavigator;
			if (e.selectedIndex<0) { return; }
			for (var i:uint=0; i<e.numChildren; i++) {
				e.getTabAt(i).selected=(i==e.selectedIndex);
			}
		}
	}


	public function togglePanel():void {
		if (stack.selectedChild!=editorContainer) {
			stack.selectedChild=editorContainer;
		}
	}

	public function selectAdvancedPanel():void { }

      private function checkAdvanced():void { }

      private var listeningToRelations:Array = [];

      private function setupAdvanced(entity:Entity):void {}

	private function setupMultiAdvanced(entity:Entity):void { }

	  public function addNewTag():void { }

      private function addedToRelation(event:RelationMemberEvent):void {
         resetRelationsGrid(selectedEntity);
      }

      private function removedFromRelation(event:RelationMemberEvent):void {
         resetRelationsGrid(selectedEntity);
      }

      private function removeRelationListeners():void {
          for each( var rel:Relation in listeningToRelations ) {
              rel.removeEventListener(Connection.TAG_CHANGED, relationTagChanged);
              rel.removeEventListener(Connection.RELATION_MEMBER_ADDED, entityRelationMemberChanged);
              rel.removeEventListener(Connection.RELATION_MEMBER_REMOVED, entityRelationMemberChanged);
          }
          listeningToRelations = [];
      }

      private function resetRelationsGrid(entity:Entity):void {
      }

      private function relationRoleChanged(event:DataGridEvent):void {
      }

      private function relationTagChanged(event:TagEvent):void {
      }

      private function entityRelationMemberChanged(event:RelationMemberEvent):void {
      }

      private function checkMembers():void {
      }


      private function editRelation(id:Number):void {
          var panel:RelationEditorPanel = RelationEditorPanel(
              PopUpManager.createPopUp(Application(FlexGlobals.topLevelApplication), RelationEditorPanel, true));
          panel.setRelation(connection.getRelation(id));
          PopUpManager.centerPopUp(panel);
      }

	/** Create relation actions menu */

	public function createRelationMenu(button:PopUpButton):void {
		var menu:Menu = new Menu(); 
		var dp:Object = [ {label: "Select all members"}, 
						  {label: "Deselect all members"}, 
						  {label: "Add selection to this relation", enabled: false},
						  {label: "Delete relation"} ]; 
		menu.dataProvider = dp; 
		menu.addEventListener("itemClick", selectRelationMenu); 
		button.popUp = menu;
	}

	/** Enable 'add selection to...' entry only if some of the selection isn't in the relation.
	    Called each time the menu is clicked. */
	
	public function updateRelationMenu(event:Event):void {
		rowData=event.target.parent.data;	// this makes it accessible from selectRelationMenu
		var menu:Menu = Menu(event.target.popUp);
		var enable:Boolean = false;
		if (selectedEntity != null && selectedEntity is EntityCollection) {
			// Enable only if some entities aren't a member of the relation
			enable=!rowData.universal;
		}
		if (enable==menu.dataProvider[2].enabled) return;
		menu.dataProvider[2].enabled=enable;
		menu.invalidateList();
	}

	/** Do the action selected in the relation actions menu */

	public function selectRelationMenu(event:MenuEvent):void {
		var rel:Relation=rowData.relation;
		var entities:Array;
		var controller:EditController=FlexGlobals.topLevelApplication.theController;
		switch (event.index) {
			case 0: // Select all members
				entities=selectedEntity.entities.concat(rel.memberEntities);
				entities=entities.filter(function(e:*, i:int, arr:Array):Boolean { return arr.indexOf(e) == i } );	// remove duplicates
				// ** FIXME: This is a really horrible way of changing the controller state
				controller.setState(controller.findStateForSelection(entities));
				break;

			case 1: // Deselect all members
				entities=selectedEntity.entities;
				entities=entities.filter(function(e:*, i:int, arr:Array):Boolean { return !e.hasParent(rel) } );
				controller.setState(controller.findStateForSelection(entities));
				break;

			case 2: // Add selection to this relation
				var undo:CompositeUndoableAction=new CompositeUndoableAction("Remove selection from relations");
				for each (var entity:Entity in selectedEntity.entities) {
					if (!entity.hasParent(rel)) {
						rel.appendMember(new RelationMember(entity,''), undo.push);
					}
				}
				MainUndoStack.getGlobalStack().addAction(undo);
				break;
			
			case 3:	// Delete relation
				Alert.show("Do you really want to delete the relation?","Are you sure?",Alert.YES | Alert.CANCEL,null,
					function(event:CloseEvent):void { 
						if (event.detail==Alert.CANCEL) return;
						rel.remove(MainUndoStack.getGlobalStack().addAction);
					} );
				break;
		}
	}

      private function tagChanged(event:TagEvent):void {
          refreshFeatureIcon();
      }

      public function loadFeatures():void {
          mapFeatures = MapFeatures.getInstance();
          stack.removeChild(membersVBox); // remove by default, will be added if relation
      }

      /** Open up a new browser page, showing the help page as defined in Map Features XML file .*/
      public function openDescription():void {
          if ( feature != null && feature.hasHelpURL() )
              navigateToURL(new URLRequest(feature.helpURL), "potlatch_help");
      }

      /** Open up a new browser page showing OSM's view of the current entity. */
      public function openEntityPage():void {
          if (selectedEntity != null && selectedEntity.id >= 0) {
              // This is slightly hard-coded, but not drastically. The ../s could be changed for string manipulation of the apiBase
              var urlBase:String = connection.apiBase + '../../browse/'
              navigateToURL(new URLRequest(urlBase+selectedEntity.getType()+'/'+selectedEntity.id), "potlatch_browse");
          }
      }

      public function addToRelation():void {
          new RelationSelectPanel().init(selectedEntity,new Object());
      }

      private function removeFromRelation(id:Number, index:int=-1):void {
		var rel:Relation=connection.getRelation(id);
		if (index>-1) {
			rel.removeMemberByIndex(index, MainUndoStack.getGlobalStack().addAction);
		} else if (selectedEntity is EntityCollection) {
			var undo:CompositeUndoableAction=new CompositeUndoableAction("Remove selection from relations");
			for each (var e:Entity in EntityCollection(selectedEntity).entities) rel.removeMember(e,undo.push);
			MainUndoStack.getGlobalStack().addAction(undo);
		}
      }

      public function changeFeatureType(event:Event):void {
          if ( selectedEntity == null )
              return;

          UIComponent.suspendBackgroundProcessing();
          var newFeature:Feature = currentCategorySelector.selectedType;
          var undoStack:Function = MainUndoStack.getGlobalStack().addAction;
          var action:CompositeUndoableAction = new CompositeUndoableAction(
                  "Set "+selectedEntity.getType()+" "+selectedEntity.id+" to "+newFeature.name);
		  selectedEntity.suspend();
          
          // build a list of tags that are editable in new feature
          var editableTags:Array = new Array();
          for each( var editor:EditorFactory in newFeature.editors ) {
              if ( editor is SingleTagEditorFactory ) {
                  var singleTagEditor:SingleTagEditorFactory = editor as SingleTagEditorFactory;
                  editableTags.push(singleTagEditor.key);
              }
          }

          // remove tags from the current feature
          if ( feature != null ) {
              for each( var oldtag:Object in feature.tags ) {
                  if ( editableTags.indexOf(oldtag["k"]) < 0 ) {
                      selectedEntity.setTag(oldtag["k"], null, action.push);
                  }
              }
          }

          // set tags for new feature
          if ( newFeature != null ) {
              for each( var newtag:Object in newFeature.tags ) {
                  selectedEntity.setTag(newtag["k"], newtag["v"], action.push);
              }
          }

		  selectedEntity.resume();
          undoStack(action);
          popupChange.close();
          initialiseEditors();
          UIComponent.resumeBackgroundProcessing();
      }

	private function resizePOIGrid(event:Event):void {
		var rows:Number=event.target.dataProvider.length/event.target.columnCount;
		if (rows!=Math.floor(rows)) { rows=Math.floor(rows+1); }
		event.target.height=rows*(event.target.rowHeight+1);
	}
  ]]></fx:Script>
</mx:VBox>

